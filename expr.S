.section .rodata

cond_prompt:	.string "if x ... 16: "

prompt:		.string "x = "
expr_ge16:	.string "(x - 11)^2 + 125 = "
expr_l16:	.string "x^2 + 72x + 6400 = "

err_read:	.string "Could not read the input!\n"
err_nan:	.string "You should enter a non-negative integer!\n"

.section .data

buf:		.fill 64
buf_len = .-buf

inp_x:		.quad 1

.section .text
.global _start
_start:
	/* condition input */

	# print the condition prompt
	mov $cond_prompt, %rsi
	call print

	xor %rax, %rax		# read
	xor %rdi, %rdi		# from stdin
	mov $buf, %rsi		# into buf
	mov $buf_len-1, %rdx	# at most buf_len chars (1b reserved tor \0)
	syscall

	add $buf-1, %rax	# point at \n (buf[rax-1])
	cmpb $'\n', (%rax)
	je .cond_read_success
	mov $err_read, %rsi	# | no \n, panic with a read error
	jmp panic		# |
.cond_read_success:

	movb $0, (%rax)		# trim newline

	movw (buf), %ax
	cmp $0x3C, %al		# <x
	je .ilx
	cmp $0x3E, %al		# >x
	je .igx
	jmp _start
.ilx:
	cmp $0x3D, %ah		# <=
	je .ile
	cmp $0, %ah		# <
	je .il
	jmp _start
	.ile:
		mov $fjle, %r15
		jmp .cond_set
	.il:
		mov $fjl, %r15
		jmp .cond_set
.igx:
	cmp $0x3D, %ah		# >=
	je .ige
	cmp $0, %ah		# >
	je .ig
	jmp _start
	.ige:
		mov $fjge, %r15
		jmp .cond_set
	.ig:
		mov $fjg, %r15
		jmp .cond_set
.cond_set:

	/* value input */

	# print the prompt
	mov $prompt, %rsi
	call print

	xor %rax, %rax		# read
	xor %rdi, %rdi		# from stdin
	mov $buf, %rsi		# into buf
	mov $buf_len-1, %rdx	# at most buf_len chars (1b reserved tor \0)
	syscall

	add $buf-1, %rax	# point at \n (buf[rax-1])
	cmpb $'\n', (%rax)
	je .x_read_success
	mov $err_read, %rsi	# | no \n, panic with a read error
	jmp panic		# |
.x_read_success:

	movb $0, (%rax)		# trim newline

	mov $buf, %rsi		# convert buf
	mov $10, %rbx		# base 10
	call stoi64		# to u64

	test %rbx, %rbx		# check for stou64 errors
	jz .u64_success
	mov $err_nan, %rsi
	call print
	jmp _start
.u64_success:

	mov $16, %rbx
	mov $.imeets, %rcx
	call *%r15
	jmp .ielse
	# cmp $16, %rax
	# jl .l16

.imeets:			# input meets the condition
	pushq $expr_ge16	# use a register?

	sub $11, %rax		# t = x - 11
	mul %rax		# t = t^2
	add $125, %rax		# t += 125

	jmp .done

.ielse:
	pushq $expr_l16		# use a register?

	mov %rax, %rcx		# tmp %rcx = x

	mul %rax		# x^2
	mov %rax, %rbx		# %rbx = x^2

	mov %rcx, %rax		# | %rax = 72x
	mov $72, %rcx		# |
	mul %rcx		# |

	add %rbx, %rax		# %rbx + %rax = x^2 + 72x
	add $6400, %rax		# + 6400

.done:
	mov $10, %rbx		# base 10
	mov $buf, %rdi		# into the buf
	call u64tos		# rax to string

	# print the expression
	popq %rsi		# use a register?
	call print

	# print the result
	mov $buf, %rsi
	call print

	mov $'\n', %rax
	call putchar

	/* sys_exit */
	mov $60, %rax
	xor %rdi, %rdi
	syscall

/*
 * panic() - print an error message and exit with an error
 *
 * Args: %rsi - error message
 *
 * Never returns
 */
panic:
	mov $2, %rdi		# stderr
	call fprint

	/* sys_exit */
	mov $60, %rax
	mov $-1, %rdi
	syscall

/*
 * fj* - check if the arguments meet a condition (a <=> b).
 *       Basically a conditional jump as a function.
 *
 * Args: %rax - a
 *       %rbx - b
 *       %rcx - jmp address on success
 */
fjge:
	cmp %rbx, %rax
	jge .meets
	ret
fjg:
	cmp %rbx, %rax
	jg .meets
	ret
fje:
	cmp %rbx, %rax
	je .meets
	ret
fjne:
	cmp %rbx, %rax
	jne .meets
	ret
fjl:
	cmp %rbx, %rax
	jl .meets
	ret
fjle:
	cmp %rbx, %rax
	jle .meets
	ret
.meets:
	sub $8, %rsp		# forget the return address
	jmp *%rcx
